#!/usr/bin/perl -wT
# checks BGP states in quagga GPLv3+ started 20100320 Matija Nalis <mnalis-perl@axe.tomsoft.hr>
# note: "adduser nagios quaggavty" and restart NRPE, or it won't have permissions

use strict;
my $DEBUG = 0;
my $MIN_ADDR = 100000;	# minimum number of announced routes per BGP peer
my @OPTIONAL_AS = (34594);	# optional AS-numbers, it will give warning only if they are down
my $CMD='vtysh -c "show ip bgp summary"';

$ENV{PATH} = '/bin:/sbin:/usr/bin:/usr/sbin';

open OUTPUT, "${CMD}|" or die "can't exec $CMD: $!";

#Neighbor        V    AS MsgRcvd MsgSent   TblVer  InQ OutQ Up/Down  State/PfxRcd
#85.114.32.213   4 34594  463333    2332        0    0    0 1d10h00m Active     
#213.147.96.68   4 35549  189300    8830        0    0    0 1d12h26m   312080

#Neighbor        V    AS MsgRcvd MsgSent   TblVer  InQ OutQ Up/Down  State/PfxRcd
#85.114.32.213   4 34594  163344    1109        0    0    0 02:51:18   316672
#213.147.96.68   4 35549   81298    1832        0    0    0 07:37:15   318457

my $nagios = '';
my $perf = '';
my $total = 0;
my $warning_only = 1;

while (<OUTPUT>) {
	print "DBG: line: $_" if $DEBUG > 1;
	next unless /^(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})\s+\d+\s+(\d+)\s+\d+\s+\d+\s+\d+\s+\d+\s+\d+\s+[a-zA-Z0-9:]+\s+(.+?)\s*$/;
	my ($ip, $as, $prefixes) = ($1, $2, $3);
	print "DBG: parsed: IP=$ip, AS$as, prefixes=$prefixes\n" if $DEBUG;

	no warnings;	# $prefixes can be "Active" or other non-numeric string
	if ($prefixes < $MIN_ADDR) {
		$nagios .= "AS$as FAILED. ";
		$warning_only = 0 unless grep {/^${as}$/} @OPTIONAL_AS;
	}
	$total += $prefixes;
	use warnings;
	$perf .= "AS$as($ip)=$prefixes;"
}

if ($total < $MIN_ADDR) { $nagios = "Too little total prefixes: $total" }

if ($nagios) {	# some error
	if ($warning_only) {
		print "BGP WARNING: $nagios|$perf\n";
		exit 1;
	} else {
		print "BGP CRITICAL: $nagios|$perf\n";
		exit 2;
	}
} else {	# everything ok
	print "BGP OK: $total total prefixes|$perf\n";
	exit 0;
}
